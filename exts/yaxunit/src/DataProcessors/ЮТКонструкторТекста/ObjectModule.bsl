
#Область ОписаниеПеременных

Перем Контекст;

#КонецОбласти

#Область ПрограммныйИнтерфейс

Функция НоваяСтрока(Текст, НачалоСтроки = "", КонецСтроки = "") Экспорт
	
	ПодготовленныйТекст = НачалоСтроки + Текст + КонецСтроки;
	
	Контекст.Строки.Добавить(ПодготовленныйТекст);
	
	Возврат ЭтотОбъект;
	
КонецФункции

Функция НоваяСтрокаПоШаблону(Шаблон, П1 = "", П2 = "", П3 = "", П4 = "", П5 = "", П6 = "", П7 = "", П8 = "", П9 = "") Экспорт
	
	ПараметрыСтрокой = СтрШаблон("%1;%2;%3;%4;%5;%6;%7;%8;%9", П1, П2, П3, П4, П5, П6,  П7, П8, П9);
	ПараметрыЗаполненные  = СтрРазделить(ПараметрыСтрокой, ";", Ложь);
	
	Для Итератор = 1 По ПараметрыЗаполненные.Количество() Цикл
		ПараметрыЗаполненные[Итератор - 1] = """" + СтрЗаменить(ПараметрыЗаполненные[Итератор - 1], """", """""") + """"; // экранируем кавычки
	КонецЦикла;
	
	ПараметрыБезПустых =  СтрСоединить(ПараметрыЗаполненные, ", ");
	КодДляВыполнения = СтрШаблон("ПодготовленныйТекст = СтрШаблон(Шаблон, %1);", ПараметрыБезПустых);
	
	ПодготовленныйТекст = "";
	
	Выполнить(КодДляВыполнения);
	
	НоваяСтрока(ПодготовленныйТекст);
	
	Возврат ЭтотОбъект;
			
КонецФункции

Функция ВставитьСтроку(Позиция, Текст, НачалоСтроки = "", КонецСтроки = "") Экспорт
	
	ПодготовленныйТекст = НачалоСтроки + Текст + КонецСтроки;
	
	Контекст.Строки.Вставить(Позиция, ПодготовленныйТекст);
	
	Возврат ЭтотОбъект;
	
КонецФункции

Функция ВставитьПервуюСтроку(Текст, НачалоСтроки = "", КонецСтроки = "") Экспорт

	Возврат ВставитьСтроку(0, Текст, НачалоСтроки, КонецСтроки);
	
КонецФункции	
	
Функция ПереносСтроки() Экспорт

	ПоследнийЭлемент = Контекст.Строки.Количество() - 1;
	Контекст.Строки[ПоследнийЭлемент] = Контекст.Строки[ПоследнийЭлемент] + Символы.ПС;
	
	Возврат ЭтотОбъект;
	
КонецФункции

Функция Табуляция(Количество = 1) Экспорт
	
	Табы = Сдвиг(Количество);

	ПоследнийЭлемент = Контекст.Строки.Количество() - 1;
	Контекст.Строки[ПоследнийЭлемент] = Табы + Контекст.Строки[ПоследнийЭлемент];
	
	Возврат ЭтотОбъект;
	
КонецФункции

Функция Разделитель(Разделитель) Экспорт
	
	Контекст.Разделитель = Разделитель;
	
	Возврат ЭтотОбъект;
	
КонецФункции

Функция Текст() Экспорт
	
	Возврат СтрСоединить(Контекст.Строки, Контекст.Разделитель);
	
КонецФункции

Функция КоличествоСтрок() Экспорт
	
	Возврат Контекст.Строки.Количество();
	
КонецФункции

Функция УстановитьТекст(Текст) Экспорт
	
	СтрокиТекста = СтрРазделить(Текст, Контекст.Разделитель);
	
	Для Каждого Стр Из СтрокиТекста Цикл
		НоваяСтрока(Стр);
	КонецЦикла;
	
	Возврат ЭтотОбъект;
		
КонецФункции

#КонецОбласти

#Область СлужебныеПрограммныйИнтерфейс

Процедура Инициализировать() Экспорт
	
	Контекст = Новый Структура;
	Контекст.Вставить("Строки", Новый Массив);
	Контекст.Вставить("Разделитель", Символы.ПС);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция Сдвиг(ШагСдвига = 0)
	
	Табы = Новый Массив;
	
	Для Итератор = 1 По ШагСдвига Цикл
		Табы.Добавить("	");
	КонецЦикла;
	
	Сдвиг = СтрСоединить(Табы);
			
	Возврат Сдвиг;
	
КонецФункции

#КонецОбласти