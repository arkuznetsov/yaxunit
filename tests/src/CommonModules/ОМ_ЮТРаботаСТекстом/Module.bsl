#Область ПрограммныйИнтерфейс

Процедура ИсполняемыеСценарии() Экспорт

	ЮТТесты
		.ДобавитьТестовыйНабор("Конструктор текста")
			.ДобавитьТест("тест_ДобавлениеНовойСтроки", "Конструтор текста. Добавление новой строки")
			.ДобавитьТест("тест_ДобавлениеНовойСтрокиПоШаблону", "Конструтор текста. Добавление новой строки по шаблону")
				.СПараметрами("Сумма", "XMLЗначение(Тип(""Число""), ""1000"")").Представление("Конструтор текста. Добавление новой строки по шаблону. П1 = Сумма, П2 = 1000")
				.СПараметрами("Дата", "XMLЗначение(Тип(""Дата""), ""2025-05-30T09:00:00"")").Представление("Конструтор текста. Добавление новой строки по шаблону. П1 = Дата, П2 = XMLЗначение(Тип(""Дата""), ""2025-05-30T09:00:00"")")
			.ДобавитьТест("тест_ДобавлениеТекста", "Конструтор текста. Добавить текст")
			.ДобавитьТест("тест_ДобавлениеНовойСтрокиПоШаблонПараметровБольшеЧемВШаблоне","Конструтор текста. Добавление новой строки по шаблону. Ошибка - Слишком много параметров")
			.ДобавитьТест("тест_ВставитьПервуюСтроку", "Конструтор текста. Вставить первую строку")
			.ДобавитьТест("тест_ВставитьСтроку", "Конструтор текста. Вставить строку")
			.ДобавитьТест("тест_ПереносСтроки", "Конструтор текста. Перенос строки")
			.ДобавитьТест("тест_Табуляция", "Конструтор текста. Табуляция строки")
			.ДобавитьТест("тест_Текст", "Конструтор текста. Получить готовый текст")	
			.ДобавитьТест("тест_Разделитель", "Конструтор текста. Заменить стандартный разделитель строк")	
			.ДобавитьТест("тест_Количество", "Конструтор текста. Количество строк добавленных")	
			.ДобавитьТест("тест_УстановитьТекстИсходныйТекстСоответствует", "Конструтор текста. Установить текст - исходный текст совпадает с результатом")	
			.ДобавитьТест("тест_УстановитьТекстИсходныйТекстРазобралсяКорректно", "Конструтор текста. Установить текст - Количество строк совпадает с исходным текстом")	
	;
	
КонецПроцедуры 

#КонецОбласти

#Область Тесты

Процедура тест_ДобавлениеНовойСтроки() Экспорт

	ОжидаемыйРезультат = "1Строка2";
	
	Конструктор = ЮТРаботаСТекстом.КонструкторТекста();
	
	Конструктор
		.НоваяСтрока("Строка", "1", 2);
			
	Результат = Конструктор.Текст();
	
	ЮТест
		.ОжидаетЧто(Результат)
			.Равно(ОжидаемыйРезультат);

КонецПроцедуры

Процедура тест_ДобавлениеНовойСтрокиПоШаблону(ПараметрШаблона1, ПараметрШаблона2) Экспорт
	
	Шаблон = ".Установить(""%1"", %2)";
	
	ОжидаемыйРезультат = СтрШаблон(Шаблон, ПараметрШаблона1, ПараметрШаблона2);
	
	Конструктор = ЮТРаботаСТекстом.КонструкторТекста();
	
	Конструктор
		.НоваяСтрокаПоШаблону(Шаблон, ПараметрШаблона1, ПараметрШаблона2);
			
	Результат = Конструктор.Текст();
	
	ЮТест
		.ОжидаетЧто(Результат)
			.Равно(ОжидаемыйРезультат);
	
КонецПроцедуры

Процедура тест_ДобавлениеТекста() Экспорт
	
	Текст = "Макет = ""|Колонка1|Колонка2|Ы||1		 |	     2|"";";
	
	Конструктор = ЮТРаботаСТекстом
		.КонструкторТекста()
		.НоваяСтрока("// Первая строка");
	
	Конструктор.ДобавитьТекст(Текст, "Ы", 1);
			
	Результат = Конструктор.Текст();
	
	ОжидаемыйРезультат = "// Первая строка" + 
			Символы.ПС + Символы.Таб + "Макет = ""|Колонка1|Колонка2|" +
			Символы.ПС + Символы.Таб + "||1		 |	     2|" + """;";

	ЮТест
		.ОжидаетЧто(Результат)
			.Равно(ОжидаемыйРезультат);
	
КонецПроцедуры
			
Процедура тест_ДобавлениеНовойСтрокиПоШаблонПараметровБольшеЧемВШаблоне() Экспорт
	
	Шаблон = ".Установить(""Сумма"", %1)";
	
	Конструктор = ЮТРаботаСТекстом.КонструкторТекста();
	
	ТекстИсключения = "";
	#Если ТолстыйКлиентОбычноеПриложение Тогда	
		ТекстИсключения = "Ошибка при вызове метода контекста (СтрШаблон)";
	#ИначеЕсли ТолстыйКлиентУправляемоеПриложение ИЛИ Клиент Тогда	
		ТекстИсключения = "Слишком много фактических параметров";
	#КонецЕсли
		
	ЮТест.ОжидаетЧто(Конструктор)
		.Метод("НоваяСтрокаПоШаблону", ЮТМетоды.МассивПараметров(Шаблон, "Сумма", "XMLЗначение(Тип(""Число""), ""1000"")"))
		.ВыбрасываетИсключение(ТекстИсключения);
	
КонецПроцедуры

Процедура тест_ВставитьПервуюСтроку() Экспорт

	ОжидаемыйРезультат = "0Строка3
						|Строка1
						|Строка2";
	
	Конструктор = ЮТРаботаСТекстом.КонструкторТекста();
	
	Конструктор
		.НоваяСтрока("Строка1")
		.НоваяСтрока("Строка2")
	;
	
	Конструктор
		.ВставитьПервуюСтроку("Строка", "0", 3);
			
	Результат = Конструктор.Текст();
	
	ЮТест
		.ОжидаетЧто(Результат)
			.Равно(ОжидаемыйРезультат);

КонецПроцедуры

Процедура тест_ВставитьСтроку() Экспорт

	ОжидаемыйРезультат = "Строка1
						|0Строка3
						|Строка2";
	
	Конструктор = ЮТРаботаСТекстом.КонструкторТекста();
	
	Конструктор
		.НоваяСтрока("Строка1")
		.НоваяСтрока("Строка2")
	;
	
	Конструктор
		.ВставитьСтроку(1, "Строка", "0", 3);
			
	Результат = Конструктор.Текст();
	
	ЮТест
		.ОжидаетЧто(Результат)
			.Равно(ОжидаемыйРезультат);

КонецПроцедуры

Процедура тест_ПереносСтроки() Экспорт

	ОжидаемыйРезультат = "Строка1
						|
						|Строка2";
	
	Конструктор = ЮТРаботаСТекстом.КонструкторТекста();
	
	Конструктор
		.НоваяСтрока("Строка1")
		.ПереносСтроки()
		.НоваяСтрока("Строка2")
	;
			
	Результат = Конструктор.Текст();
	
	ЮТест
		.ОжидаетЧто(Результат)
			.Равно(ОжидаемыйРезультат);

КонецПроцедуры
		
Процедура тест_Табуляция() Экспорт

	ОжидаемыйРезультат = "		Строка1
						|Строка2";
	
	Конструктор = ЮТРаботаСТекстом.КонструкторТекста();
	
	Конструктор
		.НоваяСтрока("Строка1").Табуляция(2)
		.НоваяСтрока("Строка2")
	;
			
	Результат = Конструктор.Текст();
	
	ЮТест
		.ОжидаетЧто(Результат)
			.Равно(ОжидаемыйРезультат);

КонецПроцедуры
		
Процедура тест_Текст() Экспорт

	ОжидаемыйРезультат = "		Строка1
						|Строка2  
						|  Строка3
						|Строка4";
	
	Конструктор = ЮТРаботаСТекстом.КонструкторТекста();
	
	Конструктор
		.НоваяСтрока(ОжидаемыйРезультат);
			
	Результат = Конструктор.Текст();
	
	ЮТест
		.ОжидаетЧто(Результат)
			.Равно(ОжидаемыйРезультат);

КонецПроцедуры

Процедура тест_Разделитель() Экспорт

	ОжидаемыйРезультат = "Строка1
						|
						|Строка2";
	
	Конструктор = ЮТРаботаСТекстом.КонструкторТекста();
	
	Конструктор
		.НоваяСтрока("Строка1")
		.НоваяСтрока("Строка2")
	;
			
	Конструктор.Разделитель(Символы.ПС + Символы.ПС);
	
	Результат = Конструктор.Текст();
	
	ЮТест
		.ОжидаетЧто(Результат)
			.Равно(ОжидаемыйРезультат);

КонецПроцедуры
		
Процедура тест_Количество() Экспорт

	Конструктор = ЮТРаботаСТекстом.КонструкторТекста();
	
	Конструктор
		.НоваяСтрока("		Строка1")
		.НоваяСтрока("Строка2")
		.НоваяСтрока("  Строка3")
		.НоваяСтрока("Строка3 ")
	;
	
	Результат = Конструктор.КоличествоСтрок();
	
	ОжидаемыйРезультат = 4;
	
	ЮТест
		.ОжидаетЧто(Результат)
			.Равно(ОжидаемыйРезультат);

КонецПроцедуры

Процедура тест_УстановитьТекстИсходныйТекстСоответствует() Экспорт
	
	ОжидаемыйРезультат = "Строка1
						|Строка2
						|Строка3
						|Строка4";
	
	Конструктор = ЮТРаботаСТекстом.КонструкторТекста();
	
	Конструктор
		.УстановитьТекст(ОжидаемыйРезультат);
			
	Результат = Конструктор.Текст();
	
	ЮТест
		.ОжидаетЧто(Результат)
			.Равно(ОжидаемыйРезультат);	
КонецПроцедуры

Процедура тест_УстановитьТекстИсходныйТекстРазобралсяКорректно() Экспорт
	
	Текст = "Строка1
			|
			|Строка2
			|Строка3
			|Строка4";
	
	Конструктор = ЮТРаботаСТекстом.КонструкторТекста();
	
	Конструктор
		.УстановитьТекст(Текст);
			
	ОжидаемыйРезультат = 5;
	
	ЮТест
		.ОжидаетЧто(Конструктор.КоличествоСтрок())
			.Равно(ОжидаемыйРезультат);
			
КонецПроцедуры

#КонецОбласти
