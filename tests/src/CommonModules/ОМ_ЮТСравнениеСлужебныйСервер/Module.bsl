//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2024 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область СлужебныйПрограммныйИнтерфейс

Процедура ИсполняемыеСценарии() Экспорт
	
	ЮТТесты
		.ДобавитьТест("РазличияТаблиц_РазноеКоличествоКолонок")
			.СПараметрами(Ложь)
				.Представление("РазличияТаблиц_РазноеКоличествоКолонок")
			.СПараметрами(Истина)
				.Представление("РазличияТаблиц_РазноеКоличествоКолонок (пустые таблицы)")
		.ДобавитьТест("РазличияТаблиц_РазноеКоличествоСтрок")
		.ДобавитьТест("РазличияТаблиц_РазныеКолонки")
			.СПараметрами(Ложь)
				.Представление("РазличияТаблиц_РазныеКолонки")
			.СПараметрами(Истина)
				.Представление("РазличияТаблиц_РазныеКолонки (пустые таблицы)")
		.ДобавитьТест("РазличияТаблиц_РазныеЗначения")
	;
	
КонецПроцедуры

Процедура РазличияТаблиц_РазноеКоличествоКолонок(ПустыеТаблицы) Экспорт
	
	ПроверяемоеЗначение = НоваяТаблица(1);
	ОжидаемоеЗначение = НоваяТаблица(2);
	
	Если НЕ ПустыеТаблицы Тогда
		ПроверяемоеЗначение.Добавить();
		ОжидаемоеЗначение.Добавить();
	КонецЕсли;
	
	Различия = ЮТСравнениеСлужебныйСервер.РазличияТаблиц(ПроверяемоеЗначение, ОжидаемоеЗначение, Неопределено);
	
	ЮТест.ОжидаетЧто(Различия, "Различия таблиц")
		.ИмеетДлину(1)
		.Содержит("Разное количество колонок. Ожидали `2`, а получили `1`")
	;
	
КонецПроцедуры

Процедура РазличияТаблиц_РазноеКоличествоСтрок() Экспорт
	
	ПроверяемоеЗначение = НоваяТаблица(1);
	ОжидаемоеЗначение = НоваяТаблица(1);
	
	ПроверяемоеЗначение.Добавить();
	
	Различия = ЮТСравнениеСлужебныйСервер.РазличияТаблиц(ПроверяемоеЗначение, ОжидаемоеЗначение, Неопределено);
	
	ЮТест.ОжидаетЧто(Различия, "Различия таблиц")
		.ИмеетДлину(1)
		.Содержит("Разное количество строк. Ожидали `0`, а получили `1`")
	;
	
КонецПроцедуры

Процедура РазличияТаблиц_РазныеКолонки(ПустыеТаблицы) Экспорт
	
	ПроверяемоеЗначение = НоваяТаблица(1);
	ОжидаемоеЗначение = НоваяТаблица(1);
	ОжидаемоеЗначение.Колонки[0].Имя = "НенайденнаяКолонка";
	
	Если НЕ ПустыеТаблицы Тогда
		ПроверяемоеЗначение.Добавить();
		ОжидаемоеЗначение.Добавить();
	КонецЕсли;
	
	Различия = ЮТСравнениеСлужебныйСервер.РазличияТаблиц(ПроверяемоеЗначение, ОжидаемоеЗначение, Неопределено);
	
	ЮТест.ОжидаетЧто(Различия, "Различия таблиц")
		.ИмеетДлину(1)
		.Содержит("Проверяемая таблица не содержит колонку 'НенайденнаяКолонка'")
	;
	
КонецПроцедуры

Процедура РазличияТаблиц_РазныеЗначения() Экспорт
	
	ПроверяемоеЗначение = НоваяТаблица(1);
	ОжидаемоеЗначение = НоваяТаблица(1);
	
	ПроверяемоеЗначение.Добавить().Значение1 = 1;
	ОжидаемоеЗначение.Добавить().Значение1 = "1";
	
	Различия = ЮТСравнениеСлужебныйСервер.РазличияТаблиц(ПроверяемоеЗначение, ОжидаемоеЗначение, Неопределено);
	
	ЮТест.ОжидаетЧто(Различия, "Различия таблиц")
		.ИмеетДлину(1)
		.Содержит("Строка № 1; колонка 'Значение1'; ожидали `1`, а получили `1`")
	;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НоваяТаблица(КоличествоКолонок = 1)
	
	Таблица = Новый ТаблицаЗначений();
	
	Для Инд = 1 По КоличествоКолонок Цикл
		Таблица.Колонки.Добавить("Значение" + Инд);
	КонецЦикла;
	
	Возврат Таблица;
	
КонецФункции

#КонецОбласти
